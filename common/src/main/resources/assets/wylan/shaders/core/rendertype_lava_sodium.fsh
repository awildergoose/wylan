#version 330 core

#ifndef MAX_TEXTURE_LOD_BIAS
#error "MAX_TEXTURE_LOD_BIAS constant not specified"
#endif

#import <sodium:include/fog.glsl>
#import <sodium:include/chunk_material.glsl>
#import <wylan:include/goose_sodium.glsl>

in vec4 v_Color; // The interpolated vertex color
in vec2 v_TexCoord; // The interpolated block texture coordinates
in vec2 v_FragDistance; // The fragment's distance from the camera (cylindrical and spherical)

flat in uint v_Material;

uniform sampler2D u_BlockTex; // The block texture

uniform vec4 u_FogColor; // The color of the shader fog
uniform vec2 u_EnvironmentFog; // The start and end position for environmental fog
uniform vec2 u_RenderFog; // The start and end position for border fog
uniform float u_LavaTransitionProgress; // The lava to oil transition progress (0.0-1.0)

out vec4 fragColor; // The output fragment for the color framebuffer

void main() {
    float lodBias = _material_use_mips(v_Material) ? 0.0 : float(-MAX_TEXTURE_LOD_BIAS);

    vec4 color = texture(u_BlockTex, v_TexCoord, lodBias);
    color *= v_Color; // Apply per-vertex color modulator

    vec3 targetColor = vec3(111.0/255.0, 101.0/255.0, 61.0/255.0);
    const vec3 LUMA = vec3(0.2126, 0.7152, 0.0722);

    float lum = dot(color.rgb, LUMA);
    float targetLum = dot(targetColor, LUMA);
    float safeTargetLum = max(targetLum, 1e-4);
    vec3 targetScaled = targetColor * (lum / safeTargetLum);

    color.rgb = mix(color.rgb, targetScaled, u_LavaTransitionProgress);

#ifdef USE_FRAGMENT_DISCARD
    if (color.a < _material_alpha_cutoff(v_Material)) {
        discard;
    }
#endif

    fragColor = _linearFog(color, v_FragDistance, u_FogColor, u_EnvironmentFog, u_RenderFog);
}